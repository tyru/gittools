#!/usr/bin/env perl
use strict;
use warnings;
use utf8;

use Getopt::Long qw(:config gnu_compat no_bundling no_ignore_case);
use Pod::Usage;
use IO::Pager;

sub usage () {
    pod2usage(-verbose => 2);
}


my $needhelp;
my $no_summary;
my $no_pager;
GetOptions(
    'h|help' => \$needhelp,
    'S|no-summary' => \$no_summary,
    'P|no-pager' => \$no_pager,
) or usage;
usage if $needhelp;


die "You need to run this command from the toplevel of the working tree.\n"
    if qx(git rev-parse --show-cdup) =~ /\S/;

my $previous_summary = qx(git submodule summary);
system qw(git submodule foreach), 'git pull origin master; true';
my $current_summary = qx(git submodule summary);

if (!$no_summary
    && $previous_summary ne $current_summary
    && $current_summary ne '')
{
    local *STDOUT = $no_pager ? *STDOUT : IO::Pager::open *STDOUT;
    print $current_summary;
}

__END__

=head1 NAME

    git-subup - update all submodules and show summary


=head1 SYNOPSIS

    $ git subup

=head1 OPTIONS


=head1 AUTHOR

tyru <<%email%>>
