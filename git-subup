#!/usr/bin/env perl
use strict;
use warnings;
use utf8;

use Getopt::Long qw(:config gnu_compat no_bundling no_ignore_case);
use Pod::Usage;
use IO::Pager;
use Cwd ();
use Scope::Guard qw(guard);

sub usage () {
    pod2usage(-verbose => 2);
}


my $needhelp;
my $no_summary;
my $no_pager;
my $recursively;
my $only_summary;
GetOptions(
    'h|help' => \$needhelp,
    'S|no-summary' => \$no_summary,
    'P|no-pager' => \$no_pager,
    'r|recursive' => \$recursively,
    's|summary' => \$only_summary,
) or usage;
usage if $needhelp;


die "You need to run this command from the toplevel of the working tree.\n"
    if qx(git rev-parse --show-cdup) =~ /\S/;

my $out = qx(git config -f .gitmodules -z --get-regexp '^submodule\..+\.path\$');
my @path = map { (split /\n/)[1] } split /\0/, $out;
my $pattern = join '\|', @ARGV;
if ($pattern ne '') {
    @path = grep /$pattern/, @path;
}
unless ($only_summary) {
    my $cwd = Cwd::getcwd();
    for my $path (@path) {
        chdir $path and do {
            my $guard = guard { chdir $cwd };
            system qw(git pull origin master)
        };
    }
}

my $current_summary = qx(git submodule summary @path);
if (!$no_summary && $current_summary ne '') {
    local *STDOUT = $no_pager ? *STDOUT : IO::Pager::open *STDOUT;
    for (split /\n/, $current_summary) {
        print "\n$_" if $pattern eq '' || /^\*.*(?:$pattern)/../^\s*$/;
    }
}

__END__

=head1 NAME

    git-subup - update all submodules and show summary


=head1 SYNOPSIS

    $ git subup

=head1 OPTIONS

=over

=item -h, --help

Show this help.

=item --no-progress

Do not show the progress messages of C<git-fetch>.
See C<git-fetch>'s C<--quiet> option.
Note that C<--no-progress> affects to C<git submodule foreach> output.
--no-progress passes C<--quiet> to C<git submodule foreach>.

=item -S, --no-summary

Do not show the summary of C<git submodule summary>.

=item -P, --no-pager

Do not pipe pager.

=item -r, --recursive

Do update nested submodules.
See C<git-submodule>'s --recursive option.

=back


=head1 AUTHOR

tyru <tyru.exe@gmail.com>
